plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'am.carbox.delivery'
    compileSdk 33
    buildToolsVersion = "33.0.1"

    defaultConfig {
        applicationId "am.carbox.delivery"
        minSdk 24
        targetSdk 33
        if (project.properties["VERSION_CODE"] != null) {
            versionCode = project.properties["VERSION_CODE"] as Integer
        } else {
            versionCode = 1
        }

        if (project.properties["VERSION_NAME"] != null) {
            versionName = project.properties["VERSION_NAME"]
        } else {
            versionName = "1.0.0"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            if (System.getenv("CI") != null) {
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            } else {
                storeFile keystoreProperties['storeFile'] ? file("../" + keystoreProperties['storeFile']) : null
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), file('../proguard.cfg')
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }
    buildFeatures {
        viewBinding = true
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(':feature:splash')
    implementation project(':feature:auth:presentation')
    implementation project(':feature:car-registration')

    implementation 'com.google.dagger:hilt-android:2.46'
    kapt 'com.google.dagger:hilt-compiler:2.46'

    implementation("androidx.navigation:navigation-fragment-ktx:2.5.3")
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
